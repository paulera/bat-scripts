@echo off


REM REMEMBER TO SETUP THE "CONFIGURATION" SECTION


REM ######################## ABOUT ########################
REM #                                                     #
REM # I made this script as a quick way to get            #
REM # snapshots of my local databases. It uses the        #
REM # mysqldump command to generate a database dump       #
REM # and compress it as a password protected zip file.   #
REM #                                                     #
REM # When commiting code, I usually commit the database  #
REM # snapshot generated by this script as well.          #
REM #                                                     #
REM # ----------------------------------------------------#
REM #                                                     #
REM # My shortest-ever-version of the MIT licence, just   #
REM # to say:                                             #
REM # THE SCRIPT IS PROVIDED "AS IS", WITHOUT WARRANTY    #
REM # OF ANY KIND. IN NO EVENT SHALL THE AUTHOR BE LIABLE #
REM # FOR ANYTHING. YOU CAN USE IT AS YOU WISH.           #
REM #                                                     #
REM # ----------------------------------------------------#
REM #                                                     #
REM #        http://github.com/paulera/bat-scripts        #
REM #                                                     #
REM #######################################################









REM ############### INITIALIZATION ##############
REM #                                           #
REM # Changes in this section are not recommend #
REM # unless you know what you are doing        #
REM #                                           #
REM #############################################




REM GETS DATE AND TIME TO BUILD THE FILENAME

	For /f "tokens=1-4 delims=/ " %%a in ('date /t') do (set DT=%%c%%b%%a)
	For /f "tokens=1-2 delims=/:" %%a in ('time /t') do (set TM=%%a%%b)
	SET FILENAME=%DT%_%TM%_dump
	SET EXTZIP=.zip
	SET EXTSQL=.sql

REM GET BAT LOCATION

	SET mypath=%~dp0
	SET WORKINGDIR=%mypath:~0,-1%

REM LOOK FOR 7ZA.EXE (7-ZIP COMMAND LINE VERSION)

	IF NOT EXIST "%WORKINGDIR%\7za.exe" GOTO 7za_not_found




REM ############ CONFIGURATION ############
REM #                                     #
REM # Change the values in this section   #
REM # to adapt the script to your project #
REM #                                     #
REM #######################################




REM ZIP FILE PASSWORD

	SET ZIP_PWD=passwordhere

REM DATABASE CONFIGURATION

	SET HOST=localhost
	SET USER=paulodev_clime
	SET PASSWORD=senhaclime123
	SET DATABASE=paulodev_clime_db
	SET ENCODING=utf8

REM GENERATED FILES DESTINATION

	SET FOLDER=%WORKINGDIR%




REM ################# EXECUTION #################
REM #                                           #
REM # Changes in this section are not recommend #
REM # unless you know what you are doing        #
REM #                                           #
REM #############################################



 
REM GENERATE THE RAW DUMP

	echo.
	echo Generating database DUMP ...
	mysqldump -u%USER% -p%PASSWORD% -h%HOST% --default-character-set=%ENCODING% --add-drop-table --opt %DATABASE% -r "%FOLDER%\%FILENAME%%EXTSQL%"

	IF NOT EXIST "%FOLDER%\%FILENAME%%EXTSQL%" GOTO dump_error

	echo DUMP succesfully generated!!!

REM COMPRESS DUMP IN A PASSWORD PROTECTED ZIP FILE

	echo.
	echo Compressing dump ...
	cd %FOLDER%
	"%WORKINGDIR%\7za.exe" -p%ZIP_PWD% a "%FOLDER%\%FILENAME%%EXTZIP%" "%FOLDER%\%FILENAME%%EXTSQL%"

	IF NOT EXIST "%FOLDER%\%FILENAME%%EXTSQL%" GOTO zip_error

	echo DUMP succesfully compressed

REM DELETE THE RAW DUMP, LEAVING ONLY THE PROTECTED ZIP FILE

	echo.
	echo Deleting DUMP raw file...
	del "%FOLDER%\%FILENAME%%EXTSQL%"

	echo.
	echo Your protected dump was successfully created as
	echo %FOLDER%\%FILENAME%%EXTZIP%
	echo.

	GOTO end




REM ##########################
REM #                        #
REM #     ERROR HANDLERS     #
REM #                        #
REM ##########################

:7za_not_found
	echo 7za.exe not found. It is a requirement to this script.
	echo You can download it at http://www.7-zip.org/download.html
	echo (choose the "7-Zip Command Line Version")
	goto end

:dump_error
	echo Could not create the DUMP file.
	echo Process aborted
GOTO end

:zip_error
	echo Could not compress the dump, but the raw dump file was
	echo successfully created.
	echo It is recommended to compress it, due to the high
	echo compress rate it will have.
GOTO end


REM END POINT

:end
echo.


REM I LIKE TO PAUSE AT THE END, BECAUSE I CALL THE SCRIPT
REM DIRECTLY FROM MY DEVELOPMENT ENVIRONMENT AND DON'T WANT
REM TO CLOSE THE WINDOW BEFORE SEE THE EXECUTION MESSAGES

pause





