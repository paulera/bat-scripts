@echo off


::::: REMEMBER TO SETUP THE "CONFIGURATION" SECTION


::::: ######################## ABOUT ########################
::::: #                                                     #
::::: # I made this script as a quick way to get            #
::::: # snapshots of my local databases. It uses the        #
::::: # mysqldump command to generate a database dump       #
::::: # and compress it as a password protected zip file.   #
::::: #                                                     #
::::: # When commiting code, I usually commit the database  #
::::: # snapshot generated by this script as well.          #
::::: #                                                     #
::::: # ----------------------------------------------------#
::::: #                                                     #
::::: # My shortest-ever-version of the MIT licence, just   #
::::: # to say:                                             #
::::: # THE SCRIPT IS PROVIDED "AS IS", WITHOUT WARRANTY    #
::::: # OF ANY KIND. IN NO EVENT SHALL THE AUTHOR BE LIABLE #
::::: # FOR ANYTHING. YOU CAN USE IT AS YOU WISH.           #
::::: #                                                     #
::::: # ----------------------------------------------------#
::::: #                                                     #
::::: #        http://github.com/paulera/bat-scripts        #
::::: #                                                     #
::::: #######################################################



::::: ############ CONFIGURATION ############
::::: #                                     #
::::: # Change the values in this section   #
::::: # to adapt the script to your project #
::::: #                                     #
::::: #######################################


::::: PREFIX
::::: just something that goes at the start in the filename

SET PREFIX=dump-

::::: ZIP FILE PASSWORD
::::: comment this line to be asked everytime
::::: you run this script

::	SET ZIP_PWD=PasswordForZipFileHere

::::: DATABASE CONFIGURATION

	SET HOST=HostNameHere
	SET USER=DatabaseUsernameHere
	SET PASSWORD=DatabasePasswordHere
	SET DATABASE=DatabaseNameHere
	SET ENCODING=utf8

::::: GENERATED FILES DESTINATION
::::: (using the working directory, but you can
::::: change)

	SET FOLDER=%WORKINGDIR%





::::: ############### INITIALIZATION ###############
::::: #                                            #
::::: # Changes in this section are not encouraged #
::::: # unless you know what you are doing         #
::::: #                                            #
::::: ##############################################




::::: GETS DATE AND TIME TO BUILD THE FILENAME

	For /f "tokens=1-4 delims=/ " %%a in ('date /t') do (set DT=%%c%%b%%a)
	For /f "tokens=1-3 delims=/:" %%a in ('echo %TIME:~0,8%') do (set TM=%%a%%b%%c)
	
	IF "%1" == "" (
		SET FILENAME=%PREFIX%%DT%-%TM%
	) ELSE (
		SET FILENAME=%PREFIX%%DT%-%TM%-%1
	)
	
	
	SET EXTZIP=.zip
	SET EXTSQL=.sql

::::: GET BAT LOCATION

	SET mypath=%~dp0
	SET WORKINGDIR=%mypath:~0,-1%

::::: LOOK FOR 7ZA.EXE (7-ZIP COMMAND LINE VERSION)

	IF NOT EXIST "%WORKINGDIR%\7za.exe" GOTO 7za_not_found


::::: ################# EXECUTION #################
::::: #                                           #
::::: # Changes in this section are not recommend #
::::: # unless you know what you are doing        #
::::: #                                           #
::::: #############################################


::::: GENERATE THE RAW DUMP

	echo.
	echo Generating database DUMP ...
	mysqldump -u%USER% -p%PASSWORD% -h%HOST% --default-character-set=%ENCODING% --add-drop-table --opt %DATABASE% -r "%FOLDER%\%FILENAME%%EXTSQL%"

	IF NOT EXIST "%FOLDER%\%FILENAME%%EXTSQL%" GOTO dump_error

	echo DUMP succesfully generated!!!

::::: COMPRESS DUMP IN A PASSWORD PROTECTED ZIP FILE

	echo.
	echo Compressing dump ...
	cd %FOLDER%
	IF "%ZIP_PWD%" == "" (
		"%WORKINGDIR%\7za.exe" -mx9 -p a "%FOLDER%\%FILENAME%%EXTZIP%" "%FOLDER%\%FILENAME%%EXTSQL%"
	) ELSE (
		"%WORKINGDIR%\7za.exe" -mx9 -p%ZIP_PWD% a "%FOLDER%\%FILENAME%%EXTZIP%" "%FOLDER%\%FILENAME%%EXTSQL%"
	)
	

	IF NOT EXIST "%FOLDER%\%FILENAME%%EXTSQL%" GOTO zip_error

	echo DUMP succesfully compressed

::::: DELETE THE RAW DUMP, LEAVING ONLY THE PROTECTED ZIP FILE

	echo.
	echo Deleting DUMP raw file...
	del "%FOLDER%\%FILENAME%%EXTSQL%"

	echo.
	echo Your protected dump was successfully created as
	echo %FOLDER%\%FILENAME%%EXTZIP%
	echo.

	GOTO end




::::: ##########################
::::: #                        #
::::: #     ERROR HANDLERS     #
::::: #                        #
::::: ##########################

:7za_not_found
	echo 7za.exe not found. It is a requirement to this script.
	echo You can download it at http://www.7-zip.org/download.html
	echo (choose the "7-Zip Command Line Version")
	goto end

:dump_error
	echo Could not create the DUMP file.
	echo Process aborted
GOTO end

:zip_error
	echo Could not compress the dump, but the raw dump file was
	echo successfully created.
	echo It is recommended to compress it, due to the high
	echo compress rate it will have.
GOTO end


::::: END POINT

:end
echo.

